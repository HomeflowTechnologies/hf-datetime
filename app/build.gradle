plugins {
	id 'com.android.library'
	id 'kotlin-android'
	id 'kotlin-kapt'
	id 'kotlin-android-extensions'
	id 'maven-publish'
	id 'com.jfrog.bintray'
}

version '1.0.0-alpha1'

task sourcesJar(type: Jar) {
	archiveClassifier.set('sources')
	from android.sourceSets.main.java.srcDirs
}
ext {
	libraryName = 'HomeflowDateTime'

	bintrayRepo = 'maven'
	bintrayName = libraryName

	publishedGroupId = 'com.homeflow'
	artifactId = libraryName

	libraryDescription = 'Android DataTime (DT) is an Android library that provides an easy way to operate the date.'

	siteUrl = 'https://github.com/HomeflowTechnologies/hf-datetime'
	gitUrl = 'https://github.com/HomeflowTechnologies/hf-datetime.git'

	libraryVersion = this.version

	developerId = 'homeflowtechnologies'
	developerName = 'Homeflow Technologies'
	developerEmail = 'support@homeflowapp.com'

	licenseName = 'MIT'
	licenseUrl = 'https://github.com/HomeflowTechnologies/LICENSE'
	allLicenses = ["MIT"]
}

def pomConfig = {
	licenses {
		license {
			name licenseName
			url licenseUrl
			distribution "repo"
		}
	}
	developers {
		developer {
			id developerId
			name developerName
			email developerEmail
		}
	}
	scm {
		url siteUrl
	}
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

afterEvaluate {
	publishing {
		publications
			{
				AndroidCountryPicker(MavenPublication)
					{
						from components.release
						groupId 'com.homeflow'
						artifactId this.artifactId
						version libraryVersion
						artifact sourcesJar {
							classifier "sources"
						}
						pom.withXml {
							def root = asNode()
							root.appendNode("url", siteUrl)
							root.children().last() + pomConfig
						}
					}

			}
		repositories {
			// this works for upload but then need to publish manually
			// it is because we need to send a flag to publish when we upload as param or header of upload request
			// https://bintray.com/docs/api/#_upload_content
			// since MavenPublish does not provide any such way, we will continue using BintrayUpload
			// once we can get that then this will be the cleanest way to publish
			maven {
				name 'Bintray'
				url "https://api.bintray.com/maven/homeflowtechnologies/maven/HomeflowDateTime/"

				credentials {
					username properties.getProperty("bintray.user")
					password properties.getProperty("bintray.apikey")
				}
			}
		}
	}

	bintray {
		user = properties.getProperty("bintray.user")
		key = properties.getProperty("bintray.apikey")
		publications = ["AndroidCountryPicker"]
		pkg {
			repo = bintrayRepo
			name = bintrayName
			desc = libraryDescription
			websiteUrl = siteUrl
			vcsUrl = gitUrl
			licenses = allLicenses
			publish = true
			publicDownloadNumbers = true
			version {
				name = libraryVersion
				desc = libraryDescription
				gpg {
					sign = true //Determines whether to GPG sign the files. The default is false
					passphrase = properties.getProperty("bintray.gpg.password")
					//Optional. The passphrase for GPG signing'
				}
			}
		}
	}
}

android {
	compileSdkVersion 30
	buildToolsVersion "30.0.2"

	defaultConfig {
		minSdkVersion 23
		targetSdkVersion 30
		versionCode 1
		versionName "1.0.0-alpha1"
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = '1.8'
	}
	buildFeatures {
		viewBinding true
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.10"
	implementation 'androidx.core:core-ktx:1.3.2'
	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'com.google.android.material:material:1.2.1'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
	implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
	implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
	testImplementation 'junit:junit:4.13'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}